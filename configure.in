dnl configure.in for cvs
dnl "$CVSid$"
AC_INIT(src/cvs.h)
AC_PREREQ(2.4)dnl Required Autoconf version.
AC_CONFIG_HEADER(config.h src/options.h)

AC_CANONICAL_HOST

AC_PROG_CC

AC_AIX
AC_MINIX
AC_ISC_POSIX
if test "$ISC" = yes; then
CFLAGS="$CFLAGS -D_SYSV3"
fi

AC_PREFIX_PROGRAM(cvs)

dnl FIXME: configure normally doesn't expand these until it is too late
dnl to make any use of them
dnl
test "x$prefix" = xNONE && prefix=$ac_default_prefix
# NOTE:  don't quote the ${prefix} here -- it'll be replaced properly later
test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
# NOTE:  we hope most makefiles will have a similar line to this
bindir=$exec_prefix/bin

AC_C_CROSS

AC_C_CONST
AC_C_CHAR_UNSIGNED
AC_C_INLINE

AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_MAKE_SET

AC_PATH_PROG(perl_path, perl, no)
AC_PATH_PROG(csh_path, csh, no)

AC_SYS_INTERPRETER
if test X"$ac_cv_sys_interpreter" != X"yes" ; then
  # silly trick to avoid problems in AC macros...
  ac_msg='perl scripts using #! may not be invoked properly'
  AC_MSG_WARN($ac_msg)
fi

dnl
dnl borrowed diff stuff from the RCS configure.in (Thanks Paul!)
dnl we probably don't need the DIFF3*, DIFF_L, DIFF_SUCCESS, DIFF_TROUBLE
dnl settings just now, but we'll keep all the kruft just in case....
dnl

# Set up simple `diff' test.
echo 0 >conftest0
echo 0 >conftest0c
echo 1 >conftest1
cat >conftestok <<'EOF'
1c1
< 0
---
> 1
EOF

# WARNING:  remove config.cache if you change this option....
#
AC_ARG_WITH(diffutils,
  [  --with-diffutils        assume GNU diffutils is similarly installed],
  [with_diffutils=$withval],
  [with_diffutils='']
)
#
# NOTE:  assume they mean what they say and know what they're doing.
#
case "$with_diffutils" in
yes)
  : ${DIFF=$bindir/diff}
  : ${DIFF3=${DIFF}3}
  : ${DIFF3_BIN=1}
  : ${DIFFFLAGS=-a}
  : ${DIFF_L=1}
  : ${DIFF_FAILURE=1}
  : ${DIFF_SUCCESS=0}
  : ${DIFF_TROUBLE=2}
  ;;
no)
  # I don't know why anyone would say this, but just in case....
  : ${DIFF='diff'}
  ;;
*)
  : ${DIFF=$with_diffutils}
  : ${DIFF3=${DIFF}3}
  : ${DIFF3_BIN=1}
  : ${DIFFFLAGS=-a}
  : ${DIFF_L=1}
  : ${DIFF_FAILURE=1}
  : ${DIFF_SUCCESS=0}
  : ${DIFF_TROUBLE=2}
  ;;
esac

# date: 1995/07/12 15:17:50;  author: meyering;  state: Exp;  lines: +1 -1
# (gdiff_path): Remove gdiff from the list of programs.
# SGI's Irix includes a program named gdiff that is an X-based GUI to diff.
#
# What a pitty.  We need config.sub and config.guess just to work around this.
#
case $host_os in
  irix*)	GDIFF='';;
  *)		GDIFF='gdiff';;
esac

# Set DIFF to the name of the `diff' program to be run.
AC_SUBST(DIFF)
AC_MSG_CHECKING([diff basename])
AC_CACHE_VAL(cvs_cv_prog_diff, [
  cvs_cv_prog_diff=$DIFF
  case "$cvs_cv_prog_diff" in
  '')
    for i in gnudiff $GDIFF diff
    do
      sh -c "exec $i conftest0 conftest1" >conftestout 2>/dev/null
      case $? in
      1)
	if cmp -s conftestok conftestout
	then cvs_cv_prog_diff=$i; break
	fi
	;;
      esac
    done
    # try to get the right diff3....
    case "$cvs_cv_prog_diff" in
    g*) DIFF3="${cvs_cv_prog_diff}3";;
    esac
    ;;
  esac
])
DIFF=$cvs_cv_prog_diff
case "$DIFF" in
'') AC_MSG_ERROR(cannot find a CVS-compatible diff);;
esac
AC_MSG_RESULT($DIFF)
AC_PATH_PROG(DIFF, $DIFF, $DIFF)

# Set DIFF_SUCCESS, DIFF_FAILURE, DIFF_TROUBLE to diff's exit status
# when it finds no differences, some differences, or trouble.
AC_SUBST(DIFF_SUCCESS)
AC_MSG_CHECKING([diff success status])
AC_CACHE_VAL(cvs_cv_status_diff_success, [
  cvs_cv_status_diff_success=$DIFF_SUCCESS
  case "$cvs_cv_status_diff_success" in
  '')
    # We can't use `$DIFF conftest0 conftest0',
    # since buggy NEXTSTEP 3.0 diff silently yields exit status 2 for this.
    $DIFF conftest0 conftest0c >/dev/null 2>&1
    cvs_cv_status_diff_success=$?
    ;;
  esac
])
DIFF_SUCCESS=$cvs_cv_status_diff_success
AC_MSG_RESULT($DIFF_SUCCESS)
#
AC_SUBST(DIFF_FAILURE)
AC_MSG_CHECKING([diff failure status])
AC_CACHE_VAL(cvs_cv_status_diff_failure, [
  cvs_cv_status_diff_failure=$DIFF_FAILURE
  case "$cvs_cv_status_diff_failure" in
  '')
    $DIFF conftest0 conftest1 >/dev/null 2>&1
    cvs_cv_status_diff_failure=$?
    ;;
  esac
])
DIFF_FAILURE=$cvs_cv_status_diff_failure
AC_MSG_RESULT($DIFF_FAILURE)
#
AC_SUBST(DIFF_TROUBLE)
AC_MSG_CHECKING([diff trouble status])
AC_CACHE_VAL(cvs_cv_status_diff_trouble, [
  cvs_cv_status_diff_trouble=$DIFF_TROUBLE
  case "$cvs_cv_status_diff_trouble" in
  '')
    $DIFF conftest0 no/such/file >/dev/null 2>&1
    cvs_cv_status_diff_trouble=$?
    ;;
  esac
])
DIFF_TROUBLE=$cvs_cv_status_diff_trouble
AC_MSG_RESULT($DIFF_TROUBLE)

# Set DIFFFLAGS to the options of the `diff' program to be run.
# Use -a if possible
AC_SUBST(DIFFFLAGS)
AC_MSG_CHECKING([diff options for CVS])
AC_CACHE_VAL(cvs_cv_options_diff, [
  cvs_cv_options_diff=$DIFFFLAGS
  case "$cvs_cv_options_diff" in
  '')
    cvs_cv_options_diff=
    $DIFF -a conftest0 conftest1 >conftestout 2>conftestout2
    case $? in
    1)
      if cmp -s conftestok conftestout && test ! -s conftestout2
      then cvs_cv_options_diff=-a
      fi
      ;;
    esac
    ;;
  esac
])
DIFFFLAGS=$cvs_cv_options_diff
AC_MSG_RESULT($DIFFFLAGS)

# Set DIFF_L to 1 if diff understands the L option, 0 otherwise.
AC_SUBST(DIFF_L)
AC_MSG_CHECKING([diff -L])
AC_CACHE_VAL(cvs_cv_options_diff_l, [
  cvs_cv_options_diff_l=$DIFF_L
  case "$cvs_cv_options_diff_l" in
  '')
    cvs_cv_options_diff_l=0
    $DIFF -c -L 0 -L 1 conftest0 conftest1 >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
*** 0
--- 1
***************
*** 1 ****
! 0
--- 1 ----
! 1
EOF
      then cvs_cv_options_diff_l=1
      fi
      ;;
    esac
    ;;
  esac
])
DIFF_L=$cvs_cv_options_diff_l
case "$DIFF_L" in
1) AC_MSG_RESULT(yes);;
*) AC_MSG_RESULT(no);;
esac

# Set DIFF3 to the name of the diff3 program.
# In some systems (e.g. BSD/OS 2.0), diffutils diff3 lives in /usr/libexec.
diff3PATH=$PATH:/usr/libexec
AC_SUBST(DIFF3)
AC_MSG_CHECKING([diff3 -m])
AC_CACHE_VAL(cvs_cv_prog_diff3_bin, [
  cvs_cv_prog_diff3_bin=$DIFF3
  case "$cvs_cv_prog_diff3_bin" in
  '')
    PATH=$diff3PATH sh -c "exec diff3 -E -m -L 0 -L 1 -L 2 conftest0 conftest1 /dev/null" >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
<<<<<<< 0
0
=======
>>>>>>> 2
EOF
      then cvs_cv_prog_diff3_bin=diff3
      fi
      ;;
    esac
  ;;
  esac
])
case "$cvs_cv_prog_diff3_bin" in
?*)
  AC_MSG_RESULT(yes)
  ac_save_path=$PATH
  PATH=$diff3PATH
  dnl FIXME:  for the moment we don't really care if we don't find diff3
  AC_PATH_PROG(DIFF3, $cvs_cv_prog_diff3_bin, $cvs_cv_prog_diff3_bin)
  PATH=$ac_save_path
  ;;
'')
  AC_MSG_RESULT(no)
  AC_MSG_CHECKING([diff3 library program])
  dnl We can't use AC_PATH_PROG since we don't want to inspect /bin,
  dnl and AC_PATH_PROG uses `test'.
  AC_CACHE_VAL(cvs_cv_path_diff3_lib, [
    $DIFF conftest0 conftest1 >conftest01
    $DIFF /dev/null conftest1 >conftestn1
    for i in /usr/*lib*/*diff3*; do
      sh -c "exec $i -E conftest01 conftestn1 conftest0 /dev/null conftest1" >conftestout 2>/dev/null
      # The exit status is arbitrary!  Test the output a bit.
      if
	grep '^<<* *conftest0$' conftestout >/dev/null 2>&1 &&
	grep '^>>* *conftest1$' conftestout >/dev/null 2>&1 &&
	grep '^0a$' conftestout >/dev/null 2>&1
      then
	cvs_cv_path_diff3_lib=$i
	break
      fi
    done
  ])
  DIFF3=$cvs_cv_path_diff3_lib
  case "$DIFF3" in
  '') AC_MSG_ERROR(cannot find a working diff3 library program);;
  ?*) AC_MSG_RESULT($DIFF3);;
  esac
  ;;
esac

AC_SUBST(DIFF3_BIN)
case "$DIFF3_BIN" in
'')
  case "$cvs_cv_prog_diff3_bin" in
  '') DIFF3_BIN=0;;
  ?*) DIFF3_BIN=1;;
  esac
  ;;
esac

# Clean up simple `diff' test.
rm -f conftest*

AC_DEFINE_UNQUOTED(DIFF, "$DIFF $DIFFFLAGS")

AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h unistd.h string.h memory.h utime.h fcntl.h ndbm.h \
                 sys/param.h sys/select.h sys/time.h sys/timeb.h \
                 io.h direct.h sys/bsdtypes.h sys/resource.h)
AC_HEADER_SYS_WAIT
AC_HEADER_STAT
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_TYPE_SIGNAL
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_REPLACE_FUNCS(getwd mkdir rename strdup strstr dup2 strerror valloc waitpid memmove)
AC_CHECK_FUNCS(fchmod fsync ftime mkfifo putenv setvbuf vfork vprintf ftruncate timezone getpagesize fchdir sigaction sigprocmask sigvec sigsetmask sigblock)
AC_CHECK_FUNC(re_exec, :, LIBOBJS="$LIBOBJS regex.o")
AC_FUNC_UTIME_NULL
AC_FUNC_ALLOCA
AC_SYS_LONG_FILE_NAMES

AC_MSG_CHECKING([for working fnmatch function])
AC_CACHE_VAL(ccvs_cv_sys_working_fnmatch,
[AC_TRY_RUN([
#include <fnmatch.h>
int
main ()
{
  exit ((fnmatch ("a", "a", FNM_FILE_NAME) == 0
	 && fnmatch ("a", "b", FNM_FILE_NAME) == FNM_NOMATCH)
	? 0 : 1);
}],
ccvs_cv_sys_working_fnmatch=yes,
ccvs_cv_sys_working_fnmatch=no,
ccvs_cv_sys_working_fnmatch=no)])
if test $ccvs_cv_sys_working_fnmatch = no; then
  LIBOBJS="$LIBOBJS fnmatch.o"
fi
AC_MSG_RESULT($ccvs_cv_sys_working_fnmatch)

dnl
dnl set $(KRB4) from --with-krb4=value -- WITH_KRB4
dnl
KRB4=/usr/kerberos
define(WITH_KRB4,[
AC_ARG_WITH([krb4],
  [  --with-krb4=value       set default $(KRB4) from value],
  [KRB4=$withval],
)dnl
echo "default place for krb4 is $KRB4"
AC_SUBST(KRB4)])dnl
WITH_KRB4

AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(int)

krb_h=
AC_MSG_CHECKING([for krb.h])
AC_TRY_LINK([#include <krb.h>],[int i;],
  [krb_h=yes krb_incdir=],
  [if test "$cross_compiling" != yes && test -r $KRB4/include/krb.h; then
     hold_cflags=$CFLAGS
     CFLAGS="$CFLAGS -I$KRB4/include"
     AC_TRY_LINK([#include <krb.h>],[int i;],
       [krb_h=yes krb_incdir=$KRB4/include])
     CFLAGS=$hold_cflags
   fi])
if test -z "$krb_h"; then
  AC_TRY_LINK([#include <krb.h>],[int i;],
    [krb_h=yes krb_incdir=],
    [if test "$cross_compiling" != yes && test -r $KRB4/include/kerberosIV/krb.h; then
       hold_cflags=$CFLAGS
       CFLAGS="$CFLAGS -I$KRB4/include/kerberosIV"
       AC_TRY_LINK([#include <krb.h>],[int i;],
	 [krb_h=yes krb_incdir=$KRB4/include/kerberosIV])
       CFLAGS=$hold_cflags
     fi])
fi
AC_MSG_RESULT($krb_h)

if test -n "$krb_h"; then
  krb_lib=
  AC_CHECK_LIB(krb,main,[krb_lib=yes krb_libdir=],
    [if test "$cross_compiling" != yes && test -r $KRB4/lib/libkrb.a; then
       krb_lib=yes krb_libdir=$KRB4/lib
     fi])
  if test -n "$krb_lib"; then
    AC_DEFINE(HAVE_KERBEROS)
    test -n "${krb_libdir}" && LIBS="${LIBS} -L${krb_libdir}"
    LIBS="${LIBS} -lkrb"
    AC_CHECK_LIB(des,main,[LIBS="${LIBS} -ldes"])
    if test -n "$krb_incdir"; then
      includeopt="${includeopt} -I$krb_incdir"
      AC_SUBST(includeopt)
    fi
  fi
fi
AC_CHECK_FUNCS(krb_get_err_text)
# If we can't find connect, try looking in -lsocket, -lnsl, and -linet.
# The Irix 5 libc.so has connect and gethostbyname, but Irix 5 also has
# libsocket.so which has a bad implementation of gethostbyname (it
# only looks in /etc/hosts), so we only look for -lsocket if we need
# it.
unset ac_cv_func_connect
AC_CHECK_FUNC(connect, :, 
[case "$LIBS" in
*-lnsl*) ;;
*) AC_CHECK_LIB(nsl_s, main) ;;
esac
case "$LIBS" in
*-lnsl*) ;;
*) AC_CHECK_LIB(nsl, main) ;;
esac
case "$LIBS" in
*-lsocket*) ;;
*) AC_CHECK_LIB(socket, connect) ;;
esac
case "$LIBS" in
*-linet*) ;;
*) AC_CHECK_LIB(inet, connect) ;;
esac
unset ac_cv_func_connect
AC_CHECK_FUNCS(connect)])

AC_CHECK_FUNC(gethostname, :, LIBOBJS="$LIBOBJS hostname.o")

# If we have connect(), we want the full client & server arrangement.
if test "$ac_cv_func_connect" = yes; then
AC_DEFINE(CLIENT_SUPPORT)
AC_DEFINE(SERVER_SUPPORT)
fi

test -f src/options.h && (
  AC_MSG_WARN(saving ./src/options.h in ./src/options.h-SAVED)
  AC_MSG_WARN(You may wish to check that local options have not been lost.)
  AC_MSG_WARN(Do not re-run ./configure or ./config.status until you have....)
  cp ./src/options.h ./src/options.h-SAVED
)

AC_OUTPUT(Makefile lib/Makefile src/Makefile doc/Makefile \
	  man/Makefile contrib/Makefile contrib/pcl-cvs/Makefile \
	  examples/Makefile windows-NT/Makefile os2/Makefile stamp-h)
